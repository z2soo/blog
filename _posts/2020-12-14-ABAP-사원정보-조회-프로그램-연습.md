---
title: "ABAP 사원정보 조회 프로그램 연습"
categories: 
  - ABAP
tags:
  - sap
  - abap
toc: true
---

## 1. 결과 화면

선택조건에 맞게 조회 조건이 변경되고, 해당 조건을 가지고 데이터를 조회해 ALV에 출력하는 것이 목적이다. 만약 현재 진행 중인 프로젝트만 조회하면 아직 진행 중인 프로젝트만 출력된다. <br>

### 조건 설정 화면

![image](https://user-images.githubusercontent.com/58674365/102048559-ba47a600-3e22-11eb-9563-85d2656c825d.png)<br>
![image](https://user-images.githubusercontent.com/58674365/102048575-c29fe100-3e22-11eb-86aa-40596e967a30.png)<br>
![image](https://user-images.githubusercontent.com/58674365/102048593-c9c6ef00-3e22-11eb-92ef-d36e53fbdd2a.png)<br>

### 데이터 조회 화면

![image](https://user-images.githubusercontent.com/58674365/102049893-1ad7e280-3e25-11eb-8c2c-ebf47d464e1a.png)

<br><br>

## 2. 코드

### REPORT

```sql
*&---------------------------------------------------------------------*
*& Report ZEDU19_0010
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT ZEDU19_0010 MESSAGE-ID ZM19.

INCLUDE ZEDU19_0010TOP.
INCLUDE ZEDU19_0010SEL.
INCLUDE ZEDU19_0010F01.
INCLUDE ZEDU19_0010I01.
INCLUDE ZEDU19_0010O01.

INITIALIZATION.

AT SELECTION-SCREEN OUTPUT.
  PERFORM DYNAMIC_SCREEN.

START-OF-SELECTION.
  PERFORM GET_DATA.

END-OF-SELECTION.
  PERFORM CALL_SCREEN_100.
```

<br>

### TOP

```sql
*&---------------------------------------------------------------------*
*& Include          ZEDU19_0010TOP
*&---------------------------------------------------------------------*

* SELECTION
TABLES: ZEDUT_1920.
DATA: BEGIN OF GS_OUT,
        ZEDU       type ZEDUT_1910-ZEDU,
        ZNAME1     TYPE ZEDUT_1910-ZNAME1,
        ZPROJ      TYPE ZEDUT_1920-ZPROJ,
        ZNAME2     TYPE ZEDUT_1920-ZNAME2,
        ZCOMP      TYPE ZEDUT_1930-ZCOMP,
        ZNAME4     TYPE ZEDUT_1930-ZNAME4,
        ZMODULE    TYPE ZEDUT_1950-ZMODULE,
        ZPERIOD_S  TYPE ZEDUT_1950-ZPERIOD_S,
        ZPERIOD_E  TYPE ZEDUT_1950-ZPERIOD_E,
      END OF GS_OUT.
DATA: GT_OUT LIKE TABLE OF GS_OUT.

* 공통
DATA: OK_CODE TYPE SY-UCOMM.

* ALV
DATA : GO_CONTAINER TYPE REF TO CL_GUI_CUSTOM_CONTAINER,
       GO_GRID      TYPE REF TO CL_GUI_ALV_GRID.

DATA : GT_FCAT   TYPE LVC_T_FCAT,
       GS_FCAT   TYPE LVC_S_FCAT,
       GS_LAYOUT TYPE LVC_S_LAYO.
```

<br>

### SELECTION

```sql
*&---------------------------------------------------------------------*
*& Include          ZEDU19_0010SEL
*&---------------------------------------------------------------------*

* 선택조건
SELECTION-SCREEN BEGIN OF BLOCK BL01 WITH FRAME TITLE TEXT-001.
  SELECTION-SCREEN BEGIN OF LINE.
    PARAMETERS: P_EDU RADIOBUTTON GROUP RB01 USER-COMMAND P1 DEFAULT 'X'.
    SELECTION-SCREEN COMMENT 3(7) TEXT-T01 FOR FIELD P_EDU.

    PARAMETERS: P_PROJ RADIOBUTTON GROUP RB01.
    SELECTION-SCREEN COMMENT 15(7) TEXT-T02 FOR FIELD P_PROJ.

    PARAMETERS: P_DATE RADIOBUTTON GROUP RB01.
    SELECTION-SCREEN COMMENT 25(7) TEXT-T03 FOR FIELD P_DATE.
  SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK BL01.
SELECTION-SCREEN SKIP.


* 조회조건
SELECTION-SCREEN BEGIN OF BLOCK BL02 WITH FRAME TITLE TEXT-002.
  PARAMETERS:     P_ZEDU  TYPE ZEDUT_1910-ZEDU      MODIF ID SC1.
  SELECT-OPTIONS: P_ZPROJ FOR ZEDUT_1920-ZPROJ      MODIF ID SC2.
  PARAMETERS:     P_ZDATE TYPE ZEDUT_1950-ZPERIOD_S MODIF ID SC3 DEFAULT SY-DATUM.
  SELECTION-SCREEN SKIP.

  PARAMETERS: P_CHECK AS CHECKBOX MODIF ID SC4.
SELECTION-SCREEN END OF BLOCK BL02.
```

<br>

### PERFORM

```sql
*&---------------------------------------------------------------------*
*& Include          ZEDU19_0010F01
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*& Form DYNAMIC_SCREEN
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM DYNAMIC_SCREEN .
* 조회기준(라디오버튼) 선택에 따라 아래 조회 조건 보이는 것이 변경됨

  LOOP AT SCREEN.
    IF P_EDU = 'X'.
      IF SCREEN-GROUP1 = 'SC2' OR SCREEN-GROUP1 = 'SC3'.
        SCREEN-ACTIVE = 0.
      ENDIF.

    ELSEIF P_PROJ = 'X'.
      IF SCREEN-GROUP1 = 'SC1' OR SCREEN-GROUP1 = 'SC3'.
        SCREEN-ACTIVE = 0.
      ENDIF.

    ELSEIF P_DATE = 'X'.
      IF SCREEN-GROUP1 = 'SC1' OR SCREEN-GROUP1 = 'SC2' OR SCREEN-GROUP1 = 'SC4'.
        SCREEN-ACTIVE = 0.
      ENDIF.
    ENDIF.
    MODIFY SCREEN.
  ENDLOOP.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form GET_DATA
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM GET_DATA .

  DATA: GV_WHERE TYPE C LENGTH 300,
        GV_LINE  TYPE C.

  CLEAR: GT_OUT, GV_WHERE, GV_LINE.

* 조회 조건(사원번호, 프로젝트 ID, 프로젝트 기간) 설정
  CASE 'X'.
    WHEN P_EDU.
      IF P_ZEDU EQ SPACE.
        CONCATENATE 'A~ZEDU LIKE' `'ABAP%'` INTO GV_WHERE.
      ELSE.
        GV_WHERE = 'A~ZEDU EQ @P_ZEDU'.
      ENDIF.
    WHEN P_PROJ.
      IF P_ZPROJ EQ SPACE.
        CONCATENATE 'A~ZEDU LIKE' `'ABAP%'` INTO GV_WHERE.
      ELSE.
        GV_WHERE = 'B~ZPROJ IN @P_ZPROJ'.
      ENDIF.
    WHEN P_DATE.
      IF P_ZDATE EQ SPACE.
        CONCATENATE 'A~ZEDU LIKE' `'ABAP%'` INTO GV_WHERE.
      ELSE.
        CONCATENATE '( D~ZPERIOD_S <= @P_ZDATE AND @P_ZDATE <= D~ZPERIOD_E )'
                    'OR'
                    '( D~ZPERIOD_S <= @P_ZDATE AND D~ZPERIOD_E IS INITIAL )'
                    INTO GV_WHERE SEPARATED BY SPACE.
      ENDIF.
  ENDCASE.

* 진행중인 프로젝트만 조회 설정
  IF P_CHECK EQ 'X'.
    CONCATENATE GV_WHERE 'AND' 'D~ZPERIOD_E IS INITIAL' INTO GV_WHERE SEPARATED BY SPACE.
  ENDIF.

* 데이터 조회
  SELECT A~ZNAME1,
       B~ZPROJ, B~ZNAME2,
       C~ZCOMP, C~ZNAME4,
       D~ZMODULE, D~ZPERIOD_S, D~ZPERIOD_E, D~ZEDU
  FROM ZEDUT_1950 AS D INNER JOIN ZEDUT_1910 AS A ON D~ZEDU EQ A~ZEDU
                       INNER JOIN ZEDUT_1920 AS B ON D~ZPROJ EQ B~ZPROJ
                       INNER JOIN ZEDUT_1930 AS C ON B~ZCOMP EQ C~ZCOMP
  WHERE (GV_WHERE)
  ORDER BY D~ZPERIOD_S
  INTO CORRESPONDING FIELDS OF TABLE @GT_OUT.

* 메세지
  IF SY-SUBRC <> 0.
    MESSAGE X000.
  ELSE.
    GV_LINE = LINES( GT_OUT ).  "전체 데이터 조회시 *표시
    MESSAGE S001 WITH GV_LINE.
  ENDIF.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form CREATE_OBJECT_INSTANCE
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM CREATE_OBJECT_INSTANCE .

  CREATE OBJECT GO_CONTAINER
    EXPORTING
      CONTAINER_NAME = 'AREA_0100'.

  CREATE OBJECT GO_GRID
    EXPORTING
      I_PARENT = GO_CONTAINER.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form SET_LAYOUT
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM SET_LAYOUT .

  CLEAR GS_LAYOUT.
  GS_LAYOUT-ZEBRA = 'X'.
  GS_LAYOUT-CWIDTH_OPT = 'A'.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form SET_FILDCAT
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM SET_FILDCAT .

  CLEAR GT_FCAT.
  CLEAR GS_FCAT.
  GS_FCAT-FIELDNAME = 'ZEDU'.
  GS_FCAT-COLTEXT   = '사번'.
  APPEND GS_FCAT TO GT_FCAT.

  CLEAR GS_FCAT.
  GS_FCAT-FIELDNAME = 'ZNAME1'.
  GS_FCAT-COLTEXT   = '사원명'.
  APPEND GS_FCAT TO GT_FCAT.

  CLEAR GS_FCAT.
  GS_FCAT-FIELDNAME = 'ZPROJ'.
  GS_FCAT-COLTEXT   = '프로젝트 ID'.
  APPEND GS_FCAT TO GT_FCAT.

  CLEAR GS_FCAT.
  GS_FCAT-FIELDNAME = 'ZNAME2'.
  GS_FCAT-COLTEXT   = '프로젝트명'.
  APPEND GS_FCAT TO GT_FCAT.

  CLEAR GS_FCAT.
  GS_FCAT-FIELDNAME = 'ZCOMP'.
  GS_FCAT-COLTEXT   = '발주사 ID'.
  APPEND GS_FCAT TO GT_FCAT.

  CLEAR GS_FCAT.
  GS_FCAT-FIELDNAME = 'ZNAME4'.
  GS_FCAT-COLTEXT   = '발주사명'.
  APPEND GS_FCAT TO GT_FCAT.

  CLEAR GS_FCAT.
  GS_FCAT-FIELDNAME = 'ZMODULE'.
  GS_FCAT-COLTEXT   = '모듈'.
  APPEND GS_FCAT TO GT_FCAT.

  CLEAR GS_FCAT.
  GS_FCAT-FIELDNAME = 'ZPERIOD_S'.
  GS_FCAT-COLTEXT   = '투입시작월'.
  APPEND GS_FCAT TO GT_FCAT.

  CLEAR GS_FCAT.
  GS_FCAT-FIELDNAME = 'ZPERIOD_E'.
  GS_FCAT-COLTEXT   = '투입종료월'.
  APPEND GS_FCAT TO GT_FCAT.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form DISPALY_ALV
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM DISPALY_ALV .
  CALL METHOD GO_GRID->SET_TABLE_FOR_FIRST_DISPLAY
    EXPORTING
*     I_BUFFER_ACTIVE =
*     I_BYPASSING_BUFFER            =
*     I_CONSISTENCY_CHECK           =
*     I_STRUCTURE_NAME              =
*     IS_VARIANT      =
*     I_SAVE          =
*     I_DEFAULT       = 'X'
      IS_LAYOUT       = GS_LAYOUT
*     IS_PRINT        =
*     IT_SPECIAL_GROUPS             =
*     IT_TOOLBAR_EXCLUDING          =
*     IT_HYPERLINK    =
*     IT_ALV_GRAPHICS =
*     IT_EXCEPT_QINFO =
*     IR_SALV_ADAPTER =
    CHANGING
      IT_OUTTAB       = GT_OUT
      IT_FIELDCATALOG = GT_FCAT
*     IT_SORT         =
*     IT_FILTER       =
    EXCEPTIONS
     INVALID_PARAMETER_COMBINATION = 1
     PROGRAM_ERROR   = 2
     TOO_MANY_LINES  = 3
     OTHERS          = 4
    .
ENDFORM.

*&---------------------------------------------------------------------*
*& Form CALL_SCREEN_100
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM CALL_SCREEN_100 .
  CALL SCREEN '100'.
ENDFORM.

*&---------------------------------------------------------------------*
*& Form REFRESH_ALV
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM REFRESH_ALV .
  CALL METHOD GO_GRID->REFRESH_TABLE_DISPLAY.
ENDFORM.
```

<br>

### PBO

```sql
*&---------------------------------------------------------------------*
*& Include          ZEDU19_0010O01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Module STATUS_0100 OUTPUT
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
MODULE STATUS_0100 OUTPUT.
  SET PF-STATUS '100'.
  SET TITLEBAR '100'.
ENDMODULE.

*&---------------------------------------------------------------------*
*& Module SET_ALV OUTPUT
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
MODULE SET_ALV OUTPUT.

  IF GO_CONTAINER IS INITIAL.
* OBJECT&INSTANCE
    PERFORM CREATE_OBJECT_INSTANCE.

* FIELD CATALOG
    PERFORM SET_FILDCAT.

* LAYOUT
    PERFORM SET_LAYOUT.

* ALV
    PERFORM DISPALY_ALV.

  ELSE.
    PERFORM REFRESH_ALV.
  ENDIF.

ENDMODULE.
```

<br>

### PAI

```sql
*&---------------------------------------------------------------------*
*& Include          ZEDU19_0010I01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Module  EXIT  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE EXIT INPUT.
  CASE OK_CODE.
    WHEN 'BACK' OR 'EXIT' OR 'CANC'.
      LEAVE TO SCREEN 0.
  ENDCASE.
ENDMODULE.
```

<br><br>

## 3. 수정 사항

### 1) 사번 조회 조건

사번 조회 조건을 parameter로 두고 조건을 추가하는 것 보다, select-options를 사용하되 no interval로 사용하도록 한다. 

```sql
# 기존
PARAMETERS: P_ZEDU  TYPE ZEDUT_1910-ZEDU MODIF ID SC1.

# 수정
SELECT-OPTIONS : s_zedu FOR zedut_1910-zedu NO INTERVALS NO-EXTENSION MODIF ID m01.
```

<br>

### 2) Default 값 설정

default 값을 select-options에 설정하지 말고  initialization 초기화에서 지정해주도록 한다. 

```sql
# 기존
PARAMETERS: P_ZDATE TYPE ZEDUT_1950-ZPERIOD_S MODIF ID SC3 DEFAULT SY-DATUM.

# 수정
INITIALIZATION.
	P_PERIOD = SY-DATUM(6).
```

<br>

### 3) 기타 

- ALV

  - custom container 보다 docking이나 split container를 사용할 것 
  
- Dynamic sql

  - Dynamic query 사용은 위험하니 최대한 지양
  - 덤프가 날 수 있음
  - 운영 중 특정 테이블 필드 값의 사용처를 찾을 때, 검색이 안됨

- Consistency Check

  - ALV에 대한 전반적인 내용을 볼 수 있음

  - ALV 출력화면에서 Shift + 우측 마우스 두번 클릭

- Field catalog - SCRTEXT_L, SCRTEXT_M, SCRTEXT_S

  - Field catalog 설정시, reference table 및 field를 선언하지 않으면 warning 발생
  - Element에서 Heading이 컬럼명이고 그 곳에서 short, mid, long 설정해주면 그 값으로 들어감

- 날짜 계산

  - 만약 202001인데 한 달 전으로 입력되게 하고 싶다면, 뒤에 01을 넣어 1달의 첫 날로 만든 후 1을 빼면 됨

    ``` sql
    DATA: LV_dATA TYPE SY-DATUM.
    
    LV_DATUM = SY-DATUM(6) && '01'.
    LV_DATUM = LV_DATUM - 1.
    P_PERIOD = LV_DATUM(6)
    ```

