---
title: "ABAP 프로젝트 조회 프로그램"
categories: 
  - ABAP
tags:
  - abap
  - sap
  - dynamic screen
  - dynamic table
  - cell color
toc: true
---

## 1. 프로젝트 조회 프로그램 생성

우선 아래 정보로 DB에 테이블 및 데이터가 생성되었다는 가정 하에 프로그램 개발을 진행한다. 최종적으로 구현하고자 하는 프로그램 화면과 기능은 아래와 같다. 

![image](https://user-images.githubusercontent.com/58674365/102738255-e916cc80-438c-11eb-9abd-a4cd3400dc5d.png)<br>

### Selection 화면 및 결과화면

![image](https://user-images.githubusercontent.com/58674365/103210791-0da11480-494a-11eb-91d2-826a1f0a12af.png)

상세하게 설명하자면 다음과 같은 조건이 있다.

- 조회 기간은 최대 12개월까지 가능하되 조회하는 개월 수 만큼 년월로써 출력 화면이 나타나야 한다. 
- 기간동안 프로젝트가 진행 중이면 '진행', 완료되었다면 마지막 종료 연월에 '종료' 및 cell 색상 설정을 해준다. 
- '프로젝트 현황' 이라는 small title 또한 grid에 추가한다. 
- 사원 아이디와 프로젝트 아이디는 출력하되 No Out으로 설정한다.

Dynamic table, 즉 동적 테이블 생성 메소드를 사용하면 보다 빠르게 작업이 가능하나 이번에는 dynamcic table을 사용하지 않고 dynamic screen을 뿌리는 작업을 해보도록 한다. 

<br><br>

## 2. 실행화면

코드를 작성하여 실행한 결과 다음과 같은 화면 및 기능을 구현했다. 

### Selection screen

![image](https://user-images.githubusercontent.com/58674365/103200687-59938f80-4931-11eb-84b0-bab5c61fb9ca.png)<br>

### 1) 단일 연월 데이터 조회

From 연월만 입력하고 데이터를 조회하면 해당 연월에 대한 데이터가 조회된다. 만약 해당 연월이 종료 시기라면 cell 색상이 빨간색으로 조회된다. 

![image](https://user-images.githubusercontent.com/58674365/103200736-83e54d00-4931-11eb-9699-53ba793c0cef.png)<br>

### 2) 연월 기간 데이터 조회

From ~ To 연월 기간 데이터를 조회하되, 12개월이 넘어가면 최대 12개월만 조회가 된다. 이 때, 조회되는 기간에 따라 Field / Column 의 값도 dynamic 하게 보여진다. 

![image](https://user-images.githubusercontent.com/58674365/103200751-8cd61e80-4931-11eb-9727-ee4c2e54df7a.png)<br>

### 3) 오류 화면

Selection 화면에서 조회 기간에 대해 정상적인 날짜인지 체크가 이루어진다. 

![image](https://user-images.githubusercontent.com/58674365/103200765-96f81d00-4931-11eb-9e76-5c221a4932bc.png)

<br><br>

## 3. 코드

### REPORT PROGRAM

```mysql
*&---------------------------------------------------------------------*
*& Report      : REPORT ZEDU19_0030.
*& DESCRIPTION : Excel Upload Program 2
*& MODULE      : EDU
*& DEVELOPER   : ABAP019
*& CREATED DATE: 2020.12.21
*----------------------------------------------------------------------*
*& 임지수
*&---------------------------------------------------------------------*
*& MODIFIED HISTORY :
*& DEVELOPER       DATE             DESCRIPTION
*----------------------------------------------------------------------*
*  ABAP019         2020.12.21       신규 개발
*&---------------------------------------------------------------------*
REPORT ZEDU19_0030 MESSAGE-ID ZM1930.

INCLUDE ZEDU19_0030TOP.
INCLUDE ZEDU19_0030SEL.
INCLUDE ZEDU19_0030F01.
INCLUDE ZEDU19_0030I01.
INCLUDE ZEDU19_0030O01.

*&=====================================================================*
*& INITIALIZATION
*&=====================================================================*
INITIALIZATION.

*&=====================================================================*
*& AT SELECTION-SCREEN
*&=====================================================================*
AT SELECTION-SCREEN.
  PERFORM CHECK_SELECT_OPTIONS.

*&=====================================================================*
*& START-OF-SELECTION
*&=====================================================================*
START-OF-SELECTION.
  PERFORM GET_DATA.

* ALV 출력 데이터 작업중에 CELL COLOR 설정도 같이 했음
* 그러나 만약 다른 방식으로 작업하고 싶다면
* SET_CELL_COLOR 부분 주석처리하고 아래 PERFORM 코드 사용하기
*  PERFORM SET_CELL_COLOR_VER_2.

*&=====================================================================*
*& END-OF-SELECTION
*&=====================================================================*
END-OF-SELECTION.
  CHECK SY-BATCH IS INITIAL.
  CALL SCREEN 100.
```

<br>

### SCREEN 0100

```mysql
PROCESS BEFORE OUTPUT.
  MODULE STATUS_0100.
  MODULE SET_ALV_0100.

PROCESS AFTER INPUT.
  MODULE EXIT AT EXIT-COMMAND.
```

<Br>

### TOP

```mysql
*&---------------------------------------------------------------------*
*& Include          ZEDU19_0030TOP
*&---------------------------------------------------------------------*

* DB MASTER 데이터
DATA: GS_ZEDUT1910 TYPE ZEDUT_1910,
      GS_ZEDUT1920 TYPE ZEDUT_1920,
      GS_ZEDUT1950 TYPE ZEDUT_1950.
DATA: GT_ZEDUT1910 LIKE TABLE OF GS_ZEDUT1910,
      GT_ZEDUT1920 LIKE TABLE OF GS_ZEDUT1920,
      GT_ZEDUT1950 LIKE TABLE OF GS_ZEDUT1950.

* 데이터 가공
DATA: BEGIN OF GS_DATE,
        S_DATE_S   TYPE SY-DATUM,
        S_DATE_E   TYPE SY-DATUM,
        S_DIFF     TYPE I,
      END OF GS_DATE.
DATA: GV_LOW       TYPE SY-DATUM,
      GV_HIGH      TYPE SY-DATUM,
      GV_DIFF      TYPE I,
      LV_NUM(2)    TYPE C,
      LV_FIELDNAME TYPE STRING,
      LV_CHECK     TYPE SY-DATUM.
FIELD-SYMBOLS: <FS>.

* ALV 출력 데이터
DATA: BEGIN OF GS_RESULT,
        ZPERIOD_S   TYPE ZEDUT_1950-ZPERIOD_S,     "확인차 넣음
        ZPERIOD_E   TYPE ZEDUT_1950-ZPERIOD_E,     "확인차 넣음
        ZEDU        TYPE ZEDUT_1910-ZEDU,
        ZNAME1      TYPE ZEDUT_1910-ZNAME1,
        ZPROJ       TYPE ZEDUT_1920-ZPROJ,
        ZNAME2      TYPE ZEDUT_1920-ZNAME2,
        ZFIELD1     TYPE STRING,
        ZFIELD2     TYPE STRING,
        ZFIELD3     TYPE STRING,
        ZFIELD4     TYPE STRING,
        ZFIELD5     TYPE STRING,
        ZFIELD6     TYPE STRING,
        ZFIELD7     TYPE STRING,
        ZFIELD8     TYPE STRING,
        ZFIELD9     TYPE STRING,
        ZFIELD10    TYPE STRING,
        ZFIELD11    TYPE STRING,
        ZFIELD12    TYPE STRING,
        CELLCOLOR   TYPE LVC_T_SCOL.
DATA: END OF GS_RESULT.
DATA: GT_RESULT LIKE TABLE OF GS_RESULT.

* ALV 관련
DATA: GO_DOCKING  TYPE REF TO CL_GUI_DOCKING_CONTAINER,
      GO_GRID     TYPE REF TO CL_GUI_ALV_GRID.
DATA : GT_FCAT    TYPE LVC_T_FCAT,
       GS_FCAT    TYPE LVC_S_FCAT,
       GS_LAYOUT  TYPE LVC_S_LAYO,
       GS_VARIANT TYPE DISVARIANT,
       GT_COLOR   TYPE LVC_T_SCOL,
       GS_COLOR   TYPE LVC_S_SCOL,
       GV_COL     TYPE STRING.


* FIELD CATALOG 관련
DATA: GV_COLTEXT   TYPE SY-DATUM,
      GV_COLTEXT_F TYPE SY-DATUM,
      GV_NUM(2)    TYPE C,
      GV_FIELDNAME TYPE STRING.

* 기타
DATA: OK_CODE   TYPE SY-UCOMM,
      GV_TITLE  TYPE SY-TITLE,
      GV_LINE   TYPE I,
      GV_COLOR_VER2 TYPE LVC_S_SCOL,
      GV_INDEX      TYPE SY-TABIX.
```

<br>

### SELECTION

```mysql
*&---------------------------------------------------------------------*
*& Include          ZEDU19_0030SEL
*&---------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK BL01 WITH FRAME TITLE TEXT-001.
  SELECT-OPTIONS: S_DATE  FOR SY-DATUM(6) NO-EXTENSION
                                          OBLIGATORY,
                  S_ZEDU  FOR GS_ZEDUT1950-ZEDU,
                  S_ZPROJ FOR GS_ZEDUT1950-ZEDU.
SELECTION-SCREEN END OF BLOCK BL01.
```

<br>

### PERFORM

```mysql
*&---------------------------------------------------------------------*
*& Include          ZEDU19_0030F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form CREATE_OBJECT_INSTANCE
*&---------------------------------------------------------------------*
FORM CREATE_OBJECT_INSTANCE .
  CREATE OBJECT GO_DOCKING
    EXPORTING
      SIDE         = CL_GUI_DOCKING_CONTAINER=>DOCK_AT_LEFT
      EXTENSION    = 3000.

  CREATE OBJECT GO_GRID
    EXPORTING
      I_PARENT     = GO_DOCKING.
ENDFORM.

*&---------------------------------------------------------------------*
*& Form SET_FILDCAT
*&---------------------------------------------------------------------*
FORM SET_FILDCAT .

* 조회 기간 상관없는 부분 FIELD CATALOG 생성
  DEFINE _FCAT.
    CLEAR: GS_FCAT.
    GS_FCAT-FIELDNAME = &1.
    GS_FCAT-COLTEXT   = &2.
    GS_FCAT-KEY       = &3.
    GS_FCAT-NO_OUT    = &4.
    APPEND GS_FCAT TO GT_FCAT.
  END-OF-DEFINITION.

  _FCAT: "'ZPERIOD_S' '투입월' ' ' ' ',
         "'ZPERIOD_E' '종료월' ' ' ' ',
         'ZEDU' '사원번호' 'X' 'X',
         'ZNAME1' '사원명' ' ' ' ',
         'ZPROJ' '프로젝트 ID' 'X' 'X',
         'ZNAME2' '프로젝트명' ' ' ' '.

* 조회 기간에 따른 추가 FIELD CATALOG 생성
  GV_LOW = GS_DATE-S_DATE_S && '01'.
  GV_COLTEXT = GV_LOW.
  GV_NUM = 1.

  DO GS_DATE-S_DIFF TIMES.
    GV_FIELDNAME = 'ZFIELD'&& GV_NUM.
    GV_COLTEXT_F = GV_COLTEXT(6).

    CLEAR: GS_FCAT.
    GS_FCAT-FIELDNAME = GV_FIELDNAME.
    GS_FCAT-COLTEXT   = GV_COLTEXT_F.
    GS_FCAT-KEY       = ' '.
    APPEND GS_FCAT TO GT_FCAT.

    GV_NUM = GV_NUM + 1.

    PERFORM ADD_ONE_MONTH USING GV_COLTEXT.

  ENDDO.
ENDFORM.

*&---------------------------------------------------------------------*
*& Form SET_LAYOUT
*&---------------------------------------------------------------------*
FORM SET_LAYOUT .

  GS_LAYOUT-ZEBRA      = 'X'.
  GS_LAYOUT-CWIDTH_OPT = 'A'.
  GS_LAYOUT-SEL_MODE   = 'D'.
  GS_LAYOUT-GRID_TITLE = TEXT-004.
  GS_LAYOUT-SMALLTITLE = 'X'.
  GS_LAYOUT-CTAB_FNAME = 'CELLCOLOR'.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form DISPLAY_ALV_0100
*&---------------------------------------------------------------------*
FORM DISPLAY_ALV_0100 .

  CALL METHOD GO_GRID->SET_TABLE_FOR_FIRST_DISPLAY
    EXPORTING
      IS_LAYOUT                     = GS_LAYOUT
      IS_VARIANT                    = GS_VARIANT
      I_SAVE                        = 'X'
    CHANGING
      IT_OUTTAB                     = GT_RESULT
      IT_FIELDCATALOG               = GT_FCAT.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form GET_DATA
*&---------------------------------------------------------------------*
FORM GET_DATA .

* SELECT CONDITION에 맞는 MASTER 데이터
  PERFORM GET_SELECTED_DATA.

* 날짜에 맞는 정보: GS_DATE 값을 기준으로 체크
  PERFORM SET_PERIOD_DATA.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form GET_SELECTED_DATA
*&---------------------------------------------------------------------*
FORM GET_SELECTED_DATA .

* 사원번호, 프로젝트 ID에 맞는 정보
  SELECT A~ZEDU A~ZNAME1
         B~ZPROJ B~ZPERIOD_S B~ZPERIOD_E
         C~ZNAME2
    INTO CORRESPONDING FIELDS OF TABLE GT_RESULT
    FROM ZEDUT_1910 AS A INNER JOIN ZEDUT_1950 AS B
                         ON A~ZEDU EQ B~ZEDU
                         INNER JOIN ZEDUT_1920 AS C
                         ON B~ZPROJ EQ C~ZPROJ
    WHERE A~ZEDU  IN S_ZEDU AND
          B~ZPROJ IN S_ZPROJ.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form CHECK_SELECT_OPTIONS
*&---------------------------------------------------------------------*
FORM CHECK_SELECT_OPTIONS .

* SELECT-OPTION ITAB 가공이 헷갈려서 따로 GS_DATE 사용
  CLEAR: GS_DATE.

* HIGH 부분이 공백일 경우
  IF S_DATE-HIGH EQ SPACE.

    "LOW에 대해서만 날짜 유형 체크
    PERFORM CHECK_YEAR USING S_DATE-LOW TEXT-002.

    "값 채워주기
    CLEAR GS_DATE.
    GV_DIFF = 1.
    GS_DATE-S_DATE_S = S_DATE-LOW.
    GS_DATE-S_DATE_E = S_DATE-LOW.
    GS_DATE-S_DIFF   = GV_DIFF.

* HIGH 부분이 공백이 아닐 경우
  ELSEIF S_DATE-HIGH NE SPACE.

*    LOW와 HIGH 둘 다에 대해 날짜 유형 체크
    PERFORM: CHECK_YEAR USING S_DATE-LOW TEXT-002,
             CHECK_YEAR USING S_DATE-HIGH TEXT-003.

*    12개월 체크
*    LOW > HIGH 경우는 SELECT OPTIONS 프로그램에서 자동 체크
    CLEAR: GV_DIFF.
    GV_LOW  = S_DATE-LOW  && '01'.
    GV_HIGH = S_DATE-HIGH && '01'.

    CALL FUNCTION 'RH_PM_CONVERT_DATE_TO_MONTH'
      EXPORTING
        BEGDA               = GV_LOW
        ENDDA               = GV_HIGH
     IMPORTING
       MONTHS              = GV_DIFF.

    IF GV_DIFF >= 12.
*      12개월보다 더 많이 입력해도 프로그램은 실행
      MESSAGE S003 DISPLAY LIKE 'E'.
      GV_DIFF = 11.
    ENDIF.

*    값 채워주기
    CLEAR GS_DATE.
    GS_DATE-S_DATE_S = GV_LOW(6).
    GS_DATE-S_DATE_E = GV_HIGH(6).
    GS_DATE-S_DIFF   = GV_DIFF + 1.

  ENDIF.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form CHECK_YEAR
*&---------------------------------------------------------------------*
FORM CHECK_YEAR  USING P_DATE
                       P_TEXT.

    IF P_DATE(1) EQ '0'.
      MESSAGE E001 WITH P_TEXT.
    ELSEIF P_DATE+4(2) EQ '00' OR P_DATE+4(2) > 12.
      MESSAGE E002 WITH P_TEXT.
    ENDIF.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form SET_PERIOD_DATA
*&---------------------------------------------------------------------*
FORM SET_PERIOD_DATA .

  LOOP AT GT_RESULT ASSIGNING FIELD-SYMBOL(<GS_RESULT>).

* 조회 날짜에 해당하는 데이터만 선택
    IF ( GS_DATE-S_DATE_S <= <GS_RESULT>-ZPERIOD_S AND <GS_RESULT>-ZPERIOD_S <= GS_DATE-S_DATE_E )
        OR ( GS_DATE-S_DATE_S <= <GS_RESULT>-ZPERIOD_E AND <GS_RESULT>-ZPERIOD_E <= GS_DATE-S_DATE_E )
        OR ( GS_DATE-S_DATE_S > <GS_RESULT>-ZPERIOD_S AND <GS_RESULT>-ZPERIOD_E EQ SPACE ).

      LV_NUM = 1.                           "FIELD명 체크 위함
      LV_CHECK = GS_DATE-S_DATE_S && '01'.  "월별로 체크하기 위함

* 조회 기간 '월' 수 만큼 체크
      DO GS_DATE-S_DIFF TIMES.

*       종료/진행 데이터 입력될 필드
        LV_FIELDNAME = '<GS_RESULT>-ZFIELD' && LV_NUM.
        ASSIGN (LV_FIELDNAME) TO <FS>.

*       아직 진행중인 프로젝트
        IF <GS_RESULT>-ZPERIOD_E IS INITIAL.
          IF LV_CHECK >= <GS_RESULT>-ZPERIOD_S.
            <FS> = '진행'.
          ENDIF.

*       종료된 프로젝트
        ELSE.
          IF <GS_RESULT>-ZPERIOD_E EQ LV_CHECK(6).
            <FS> = '종료'.

*           데이터 다 넣은 후 작업해도 ok
*           but, 데이터 다시 읽어서 종료 확인하는 작업은 효율에 좋지 않을 것 같음
*           따라서 종료 확인과 동시에 CELL COLOR 작업 진행
            GV_COL = 'ZFIELD' && LV_NUM.
            PERFORM SET_CELL_COLOR USING <GS_RESULT>
                                          GV_COL.

          ELSEIF <GS_RESULT>-ZPERIOD_S <= LV_CHECK(6) AND
                 <GS_RESULT>-ZPERIOD_E > LV_CHECK(6).
            <FS> = '진행'.
          ELSE.
            <FS> = ''.
          ENDIF.
        ENDIF.

        LV_NUM = LV_NUM + 1.
        PERFORM ADD_ONE_MONTH USING LV_CHECK.

      ENDDO.
    ELSE.
      DELETE GT_RESULT INDEX SY-TABIX.
    ENDIF.
  ENDLOOP.

  PERFORM SHOW_MESSAGE.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form ADD_ONE_MONTH
*&---------------------------------------------------------------------*
FORM ADD_ONE_MONTH  USING P_DATE TYPE SY-DATUM.

  CALL FUNCTION 'RP_CALC_DATE_IN_INTERVAL'
    EXPORTING
      DATE        =  P_DATE
      DAYS        =  '00'
      MONTHS      =  '01'
      SIGNUM      =  '+'
      YEARS       =  '00'
    IMPORTING
      CALC_DATE   = P_DATE.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form SET_VARIANT
*&---------------------------------------------------------------------*
FORM SET_VARIANT .
  GS_VARIANT-REPORT   = SY-REPID.
  GS_VARIANT-USERNAME = SY-UNAME.
ENDFORM.

*&---------------------------------------------------------------------*
*& Form SET_CELL_COLOR
*&---------------------------------------------------------------------*
FORM SET_CELL_COLOR  USING P_GS_RESULT LIKE LINE OF GT_RESULT
                           LV_COL.

  CLEAR: GS_COLOR, P_GS_RESULT-CELLCOLOR.
  GS_COLOR-FNAME = LV_COL.
  GS_COLOR-COLOR-COL = '6'.
  GS_COLOR-COLOR-INT = '0'.
  GS_COLOR-COLOR-INV = '0'.
  APPEND GS_COLOR TO P_GS_RESULT-CELLCOLOR.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form SHOW_MESSAGE
*&---------------------------------------------------------------------*
FORM SHOW_MESSAGE .

  CLEAR GV_LINE.
  GV_LINE = LINES( GT_RESULT ).

  IF SY-SUBRC <> 0.
    MESSAGE E004.
  ELSE.
    MESSAGE S005 WITH GV_LINE.
  ENDIF.

ENDFORM.


*&---------------------------------------------------------------------*
*& Form SET_CELL_COLOR_VER_2
*&---------------------------------------------------------------------*
FORM SET_CELL_COLOR_VER_2 .

  CLEAR: GV_INDEX, GV_COLOR_VER2, GS_RESULT.
  LOOP AT GT_RESULT INTO GS_RESULT.

    GV_INDEX = SY-TABIX.

    CASE '종료'.
      WHEN GS_RESULT-ZFIELD1.
        PERFORM SET_CELL_COLOR_VER2_SETTING USING 'ZFIELD1'.
      WHEN GS_RESULT-ZFIELD2.
        PERFORM SET_CELL_COLOR_VER2_SETTING USING 'ZFIELD2'.
      WHEN GS_RESULT-ZFIELD3.
        PERFORM SET_CELL_COLOR_VER2_SETTING USING 'ZFIELD3'.
      WHEN GS_RESULT-ZFIELD4.
        PERFORM SET_CELL_COLOR_VER2_SETTING USING 'ZFIELD4'.
      WHEN GS_RESULT-ZFIELD5.
        PERFORM SET_CELL_COLOR_VER2_SETTING USING 'ZFIELD5'.
      WHEN GS_RESULT-ZFIELD6.
        PERFORM SET_CELL_COLOR_VER2_SETTING USING 'ZFIELD6'.
      WHEN GS_RESULT-ZFIELD7.
        PERFORM SET_CELL_COLOR_VER2_SETTING USING 'ZFIELD7'.
      WHEN GS_RESULT-ZFIELD8.
        PERFORM SET_CELL_COLOR_VER2_SETTING USING 'ZFIELD8'.
      WHEN GS_RESULT-ZFIELD9.
        PERFORM SET_CELL_COLOR_VER2_SETTING USING 'ZFIELD9'.
      WHEN GS_RESULT-ZFIELD10.
        PERFORM SET_CELL_COLOR_VER2_SETTING USING 'ZFIELD10'.
      WHEN GS_RESULT-ZFIELD11.
        PERFORM SET_CELL_COLOR_VER2_SETTING USING 'ZFIELD11'.
      WHEN GS_RESULT-ZFIELD12.
        PERFORM SET_CELL_COLOR_VER2_SETTING USING 'ZFIELD12'.
     ENDCASE.

    MODIFY GT_RESULT FROM GS_RESULT INDEX GV_INDEX TRANSPORTING CELLCOLOR.
  ENDLOOP.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form SET_CELL_COLOR_VER2_SETTING
*&---------------------------------------------------------------------*
FORM SET_CELL_COLOR_VER2_SETTING  USING P_FNAME.

  GV_COLOR_VER2-FNAME = P_FNAME.
  GV_COLOR_VER2-COLOR-COL = '6'.
  GV_COLOR_VER2-COLOR-INT = '0'.
  GV_COLOR_VER2-COLOR-INV = '0'.
  APPEND GV_COLOR_VER2 TO GS_RESULT-CELLCOLOR.

ENDFORM.
```

<br>

### PAI

```mysql
*&---------------------------------------------------------------------*
*& Include          ZEDU19_0030I01
*&---------------------------------------------------------------------*
MODULE EXIT INPUT.
  CASE OK_CODE.
    WHEN 'BACK' OR 'CANC'.
      LEAVE TO SCREEN 0.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
  ENDCASE.
ENDMODULE.
```

<br>

### PBO

```mysql
*&---------------------------------------------------------------------*
*& Include          ZEDU19_0030O01
*&---------------------------------------------------------------------*
MODULE STATUS_0100 OUTPUT.
  SET PF-STATUS '0100'.
  SET TITLEBAR '0100' WITH GS_DATE-S_DATE_S GS_DATE-S_DATE_E.
ENDMODULE.

*&---------------------------------------------------------------------*
*& Module SET_ALV_0100 OUTPUT
*&---------------------------------------------------------------------*
MODULE SET_ALV_0100 OUTPUT.

  IF GO_DOCKING IS INITIAL.

* OBJECT*INSTANCE 생성
    PERFORM CREATE_OBJECT_INSTANCE.

* FIELD CATALOG
    PERFORM SET_FILDCAT.

* VARIANT
    PERFORM SET_VARIANT.

* LAYOUT
    PERFORM SET_LAYOUT.

* DISPLAY ALV
    PERFORM DISPLAY_ALV_0100.
  ENDIF.

ENDMODULE.
```

<br><br>

## 4. 개선사항

- Screen 0100 ALV 화면에서 12개월이 초과되면 12개월만 보여지는데, title은 selection 화면에서 설정한 날짜 그대로 뜬다.  
- 2012.02 ~ 2012.12 프로젝트를 조회한다면 2012.03 ~ 2012.11 동안 진행된 프로젝트가 조회되지 않는다. `GET_PERIOD_DATA` 의 `IF문` 수정이 필요해 보인다.